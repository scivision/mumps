@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})

find_dependency(LAPACK)

include(${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@-targets.cmake)

if(TARGET MUMPS::MPISEQ)
  set(MUMPS_mpiseq_FOUND true)
else()
  find_dependency(MPI COMPONENTS C Fortran)
  find_dependency(SCALAPACK)
endif()

get_target_property(ifc_libs MUMPS::COMMON INTERFACE_LINK_LIBRARIES)
string(FIND "${ifc_libs}" "Scotch::Scotch" needs_scotch)
if(needs_scotch GREATER_EQUAL 0)
  set(MUMPS_HAVE_Scotch true)
  find_dependency(Scotch COMPONENTS ESMUMPS)
  find_dependency(METIS)
endif()

string(FIND "${ifc_libs}" "OpenMP::OpenMP_C" needs_omp)
if(needs_omp GREATER_EQUAL 0)
  set(MUMPS_HAVE_OPENMP true)
  find_dependency(OpenMP)
endif()

check_required_components(@PROJECT_NAME@)
